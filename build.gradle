plugins {
	id 'de.undercouch.download' version '4.1.1'
	id 'java'
	id 'maven-publish'
}

def ENV = System.getenv()
def build_number = ENV.BUILD_NUMBER ? ENV.BUILD_NUMBER : "local"

file('nests').eachFile {
	if (!it.name.endsWith(".nest")) {
		return
	}

	def nests = it
	def mcVer = nests.name.replace(".nest", "")

	def makeJar = task("${mcVer}-${build_number}_makeJar", type: Jar) {
		baseName "nests-${mcVer}"
		from(file(nests)) {
			into "nests"
			rename nests.name, "mappings.nest"
		}
		destinationDirectory = file("build/jars")
	}

	build.dependsOn makeJar

	publishing {
		publications {
			create("${mcVer.replace(" ", "")}_mavenJava", MavenPublication) {
				groupId 'net.ornithemc'
				artifactId "nests"
				version "${mcVer}-${build_number}"
				artifact(makeJar.archiveFile) {
					builtBy makeJar
				}
			}
		}
	}

}

publishing {
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}
